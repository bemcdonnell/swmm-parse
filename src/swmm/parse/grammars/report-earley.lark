//
//  report-earley.lark
//
//  Created: Apr 26, 2024
//  Updated: May  2, 2024
//
//  Author:  Michael E. Tryby
//           US EPA - ORD/CESER
//
//  Notes:   A report file grammar for Earley parser
//

%import common.NEWLINE                          -> _NL
%import common.WS_INLINE                        -> _SP

%ignore _SP
%ignore _SP _NL


start       :  _NL report

report      :  prologue section* epilogue

prologue    :  RECORD _NL _LINE _NL RECORD (_NL RECORD)* _NL

section     :  title heading? table

epilogue    :  RECORD _NL RECORD _NL RECORD

title       :  _ASTERISK _NL RECORD _NL _ASTERISK _NL
            |  _ASTERISK RECORD _NL RECORD _NL _ASTERISK _LINE~2 _NL

heading     :  (RECORD _NL)? (_LINE _NL)? (RECORD _NL)~1..5 _LINE _NL

table       :  RECORD (_NL RECORD)* _NL


RECORD      :  /[^\n*]+/

_LINE       :  /-+/

_ELLIPSIS   :  /\.{3,}/

_ASTERISK   :  /\*+/
